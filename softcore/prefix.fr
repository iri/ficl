\ ** 
\ ** Prefix words for ficl
\ ** submitted by Larry Hastings, larry@hastings.org
\ **
\ (jws) To make a prefix, simply create a new definition in the <prefixes> 
\ wordlist. start-prefixes and end-prefixes handle the bookkeeping

variable save-current

: start-prefixes   get-current save-current ! <prefixes> set-current ;
: end-prefixes     save-current @ set-current ;
: show-prefixes    <prefixes> >search  words  search> drop ;

start-prefixes

S" FICL_WANT_EXTENDED_PREFIX" ENVIRONMENT? drop [if]

: " ( "text" -- caddr u )
\G define " (double-quote) as an alias for s", and make it a prefix
   postpone s" ; immediate

: ' ( 'char' -- c )
\G define ' as an alias for [char] or char, and make it a prefix
   state @ if postpone [char] else char then ; immediate


: .(
\G make .( a prefix (we just create an alias for it in the prefixes list)
   postpone .( ; immediate


: \ ( "\comment" -- )
\G make \ a prefix
   postpone \ ; immediate
: // ( "//comment" -- )
\G make // as a prefix for comments.
   postpone \ ; immediate


\ ** add 0b, 0o, 0d, and 0x as prefixes 
\ ** these temporarily shift the base to 2, 8, 10, and 16 respectively
\ ** and consume the next number in the input stream, pushing/compiling
\ ** as normal
\ **
\ ** __tempbase is precompiled, see prefix.c

: 0b ( "0b..." -- n )
\G Prefix for binary numbers.
   2 __tempbase ; immediate
: 0o ( "0o..." -- n )
\G Prefix for octal numbers.
   8 __tempbase ; immediate

[endif]

: 0d ( "0d..." -- n )
\G Prefix for decimal numbers.
   10 __tempbase ; immediate
: 0x ( "0x..." -- n )
\G Prefix for hexadecimal numbers.
   16 __tempbase ; immediate

end-prefixes

